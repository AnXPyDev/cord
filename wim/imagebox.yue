wibox = require "wibox"
gears = require "gears"

cord = {
	util: require "cord.util"
	table: require "cord.table"
}

Node = require "cord.wim.node"

stylizers = {
	geometry: =>
		size = @\get_size!
		@imagebox.forced_width = size.x
		@imagebox.forced_height = size.y

	resize: =>
		@imagebox.resize = @data\get("resize")

	image: =>
		image = @data\get("image")
		@imagebox.image = image and image\get(@data\get("color"))
}

class Imagebox extends Node
	@__name: "cord.wim.imagebox"

	defaults: cord.table.crush({}, Node.defaults, {
		resize: true
	})

	new: (config, image) =>
		super(config)

		@data\set("image", image)
		@data\set("color", @style\get("color"))
		@data\set("resize", @style\get("resize") or true)

		@imagebox = wibox.widget.imagebox!
		@content = @imagebox
		@widget = @imagebox

		cord.table.crush(@stylizers, stylizers)
			
		@data\connect_signal("updated::color", () -> @\stylize("image"))
		@data\connect_signal("updated::image", () -> @\stylize("image"))
		@data\connect_signal("updated::resize", () -> @\stylize("resize"))
		@\connect_signal("geometry_changed", () -> @\stylize("geometry"))

		@\stylize!

return Imagebox
